setenv hostname zedboard

setenv kernel_addr_r 0x3000000
setenv expand_bootfile 'setenv bootfile ${hostname}/uImage'

-----

setenv fdt_addr_r 0x2a00000
setenv expand_fdtfile 'setenv fdtfile ${hostname}/zedboard-zynq7.dtb'

-----

# this needs to be loaded from mmc and into fpga before the kernel,
# .since I use the same address with the kernel
# .try also: 0x100000
#setenv bitstream_addr_r 0x3000000
setenv bitstream_addr_r 0x100000
setenv expand_bitstream 'setenv bitstream ${hostname}/fpga.bin'

----

setenv ipaddr 192.168.42.12
setenv serverip 192.168.42.1
setenv gatewayip 192.168.42.1
setenv netmask 255.255.255.0

-----

setenv kernel_netload 'tftpboot ${kernel_addr_r} ${bootfile}'
setenv fdt_netload 'tftpboot ${fdt_addr_r} ${fdtfile}'
setenv bitstream_netload_fpgaload 'if tftpboot ${bitstream_addr_r} ${bitstream}; then fpga load 0 ${bitstream_addr_r} ${filesize}; else echo no ${bitstream}; fi'
setenv netload 'run bitstream_netload_fpgaload kernel_netload fdt_netload'


-----

setenv bootargs console=ttyPS0,115200n8
setenv nfsroot /opt/poky/zedboard-rootfs

---

setenv ips_to_bootargs 'setenv bootargs ${bootargs} ip=${ipaddr}:${serverip}:${gatewayip}:${netmask}:${hostname}:${netdev}:off'

---

setenv nfs_to_bootargs 'setenv bootargs ${bootargs} nfsroot=${serverip}:${nfsroot},v3,tcp'
setenv default_to_bootargs 'setenv bootargs ${bootargs} noinitrd nohlt panic=1'

---

setenv compose_nfs_bootargs 'run ips_to_bootargs; run nfs_to_bootargs; run default_to_bootargs'
setenv nfsboot 'echo Booting from nfs...; run compose_nfs_bootargs'

---

setenv compose_nfsboot 'run nfsboot'

---

setenv compose_default 'run expand_bitstream; echo bitstream: ${bitstream}; run expand_bootfile; echo bootfile: ${bootfile}; run expand_fdtfile; echo fdtfile: ${fdtfile}; ping ${serverip}'

---

setenv uenvcmd 'run compose_default; run netload; run compose_nfsboot; printe bootargs; bootm ${kernel_addr_r} - ${fdt_addr_r}'
run uenvcmd

---------

to make it permanent:

setenv bootcmd 'run uenvcmd'
saveenv
boot

---------
